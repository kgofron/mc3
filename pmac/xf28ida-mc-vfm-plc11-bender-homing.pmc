; Homing PLC for XPD VFM benders 
; 
; This program sequences the homing of the two motors controlling the VFM
; benders. 
; 
; Motor axes:
; 4 - Upstream bender
; 5 - Downstream bender


; Initial configuration of state variables.
; Homing State P Variable
P1100 = 0

; Homing Status P Variable
P1101 = 0

DEL GAT
OPEN PLC11 CLEAR

P1101 = 1
; Use P1102 to control whether to move to next phase of homing
;P1102 = 0

;---- Configuring State ----
P1100=1
;Save high soft limits to P variables Pxx04..x19
P1106=I413
P1107=I513
;Save the low soft limits to P variables Pxx20..x35
P1122=I414
P1123=I514
;Save the home capture flags to P variables Pxx36..x51
P1138=I7042
P1139=I7112
;Store 'not flag' to use in moving off a flag in P variables Pxx52..x67
; Note that this will change once the resolvers are working correctly as we can
; get an index signal from them.
P1154=P1138^$C
P1155=P1139^$C
;Save the limit flags to P variables Pxx68..x83
P1170=I424
P1171=I524
;Save the current position to P variables Pxx84..x99
P1186=M462
P1187=M562
;Clear the soft limits
I413=0
I414=0
I513=0
I514=0

; Move each bender motor to the unbent state at its negative limit switch. 

  if (P1101=1)
		P1100=8
		; Execute the move commands for the bender motors
		M472=-100000000
		M572=-100000000

		cmd "#4J^* #5J^*"

		; Wait for the move to complete
		I5612 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5612 > 0)
		endw
		I5612 = 600000 * 8388608/I10 ; Now start checking the conditions
		while (M440=0) ; At least one motor should not be In Position
    and (M540=0) ; At least one motor should not be In Position
		and (M442=0) ; No following errors should be set for any motor
		and (M542=0) ; No following errors should be set for any motor
		and (I5612 > 0) ; Check for timeout
		and (P1101 = 1) ; Check that we didn't abort
		endw

		; Check why we left the while loop
		if (M442=1) ; If a motor hit a following error
    or (M542=1)
			P1101 = 4
		endif
		if (I5612<0 or I5612=0) ; If we timed out
			P1101 = 3
		endif

  ; Wait for user to move to next step
  ;while(P1102 = 0)
  ;endw
  ;P1102 = 0
  
    ; set up the homing capture flags
    I7042 = 1
    I7112 = 1

    ; Issue the home command to find the encoder position
		cmd "#4HM #5HM"

		; Wait for the move to complete
		I5612 = 20 * 8388608/I10 ; Small delay to start moving
		while (I5612 > 0)
		endw
		I5612 = 600000 * 8388608/I10 ; Now start checking the conditions

		while (M440=0) ; At least one motor should not be In Position
    and (M540=0) ; At least one motor should not be In Position
		and (M442=0) ; No following errors should be set for any motor
		and (M542=0) ; No following errors should be set for any motor
    and (M430=0) ; Should not stop on position limit
    and (M530=0) ; Should not stop on position limit
		and (I5612 > 0) ; Check for timeout
		and (P1101 = 1) ; Check that we didn't abort
		endw

		; Check why we left the while loop
		if (M442=1) ; If a motor hit a following error
    or (M542=1)
			P1101 = 4
		endif
    if (M430=1) ; If a motor hit a limit switch
    or (M530=1)
      P1101 = 5
    endif
		if (I5612<0 or I5612=0) ; If we timed out
			P1101 = 3
		endif

	endif


;---- Done ----
if (P1101 = 1)
	;If we've got this far without failing, set status and state done
	P1101=0
	P1100=7
endif

;---- Tidy Up ----
;Stop all motors if they don't have a following error
if (M442=0)
	cmd "#4J/"
endif
if (M542=0)
	cmd "#5J/"
endif

;Restore the high soft limits from P variables Pxx04..x19
I413=P1106
I513=P1107
;Restore the low soft limits from P variables Pxx20..x35
I414=P1122
I514=P1123
;Restore the home capture flags from P variables Pxx36..x51
I7042=P1138
I7112=P1139
;Restore the limit flags to P variables Pxx68..x83
I424=P1170
I524=P1171

; Reenable the roller translation protection PLC 

DISABLE PLC11
CLOSE


